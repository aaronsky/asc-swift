"""
App Store Connect API spec, patched for correctness. Apple doesn't host historical iterations of this file
so if the checksum is ever invalid, chances are we need to update.
"""

load("@asc_swift//bazel/specs:versions.bzl", "APP_STORE_CONNECT_API_SPEC_VERSION")
load("@aspect_bazel_lib//lib:copy_file.bzl", "copy_file")
load("@aspect_bazel_lib//lib:jq.bzl", "jq")
load("@aspect_bazel_lib//lib:write_source_files.bzl", "write_source_file")

SPEC_FILE_NAME = "app_store_connect_api_%s_openapi.json" % APP_STORE_CONNECT_API_SPEC_VERSION

filegroup(
    name = "original_spec",
    srcs = ["openapi.json"],
)

genrule(
    name = "patch_awk",
    srcs = [
        ":original_spec",
        "@asc_swift//bazel/specs:app_store_connect_api_patches_awk_prog",
    ],
    outs = ["openapi_patched_awk.json"],
    cmd = "awk -f $(location @asc_swift//bazel/specs:app_store_connect_api_patches_awk_prog) $(locations :original_spec) > $(OUTS)",
)

jq(
    name = "patch_jq",
    srcs = [":patch_awk"],
    out = "openapi_patched_jq.json",
    filter = """\
delpaths([
    ["components", "schemas", "AgeRatingDeclarationWithoutIncludesResponse"],
    ["components", "schemas", "AppCategoriesWithoutIncludesResponse"],
    ["components", "schemas", "AppCategoryWithoutIncludesResponse"],
    ["components", "schemas", "AppEncryptionDeclarationWithoutIncludesResponse"],
    ["components", "schemas", "AppPreOrderWithoutIncludesResponse"],
    ["components", "schemas", "AppStoreVersionPhasedReleaseWithoutIncludesResponse"],
    ["components", "schemas", "AppsWithoutIncludesResponse"],
    ["components", "schemas", "AppWithoutIncludesResponse"],
    ["components", "schemas", "BetaAppLocalizationsWithoutIncludesResponse"],
    ["components", "schemas", "BetaAppReviewDetailWithoutIncludesResponse"],
    ["components", "schemas", "BetaAppReviewSubmissionWithoutIncludesResponse"],
    ["components", "schemas", "BetaBuildLocalizationsWithoutIncludesResponse"],
    ["components", "schemas", "BetaGroupsWithoutIncludesResponse"],
    ["components", "schemas", "BetaLicenseAgreementWithoutIncludesResponse"],
    ["components", "schemas", "BetaTestersWithoutIncludesResponse"],
    ["components", "schemas", "BuildIconsWithoutIncludesResponse"],
    ["components", "schemas", "BuildsWithoutIncludesResponse"],
    ["components", "schemas", "BuildWithoutIncludesResponse"],
    ["components", "schemas", "BundleIdCapabilitiesWithoutIncludesResponse"],
    ["components", "schemas", "BundleIdWithoutIncludesResponse"],
    ["components", "schemas", "CertificatesWithoutIncludesResponse"],
    ["components", "schemas", "DevicesWithoutIncludesResponse"],
    ["components", "schemas", "EndUserLicenseAgreementWithoutIncludesResponse"],
    ["components", "schemas", "PreReleaseVersionsWithoutIncludesResponse"],
    ["components", "schemas", "PrereleaseVersionWithoutIncludesResponse"],
    ["components", "schemas", "ProfilesWithoutIncludesResponse"],
    ["components", "schemas", "RoutingAppCoverageWithoutIncludesResponse"],
    ["components", "schemas", "TerritoriesWithoutIncludesResponse"]
])
""",
)

copy_file(
    name = "spec_copy",
    src = ":patch_jq",
    out = SPEC_FILE_NAME,
)

filegroup(
    name = "spec",
    srcs = [":spec_copy"],
    visibility = ["//visibility:public"],
)

write_source_file(
    name = "write_spec",
    in_file = ":spec",
    out_file = "bazel/specs/com_apple_app_store_connect_api/%s" % SPEC_FILE_NAME,
    visibility = ["//visibility:public"],
)
